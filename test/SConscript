# SConscript

Import("env")

import SCons.Errors
from difflib import unified_diff


def diff_files(target, source, env):
    if len(source) != 2 or len(target) != 1:
        raise SCons.Errors.UserError("diff action must have two sources and one target")
    with open(source[0].path, "r") as f:
        a = f.readlines()
    with open(source[1].path, "r") as f:
        b = f.readlines()
    diff = "".join(unified_diff(a, b))
    with open(target[0].path, "w") as f:
        f.write(diff)
    if len(diff) != 0:
        raise SCons.Errors.BuildError(errstr="output does not match\n" + diff)


def test_kat(name, kat):
    prog = env.Program(name + ".c")
    out = env.Command(target=name + ".txt", source=prog, action="$SOURCE > $TARGET")
    env.Command(
        target=name + ".diff",
        source=[out, kat],
        action=diff_files,
    )


test_kat("test_gimli", "test_gimli.exp")
test_kat("test_lwc_hash_kat", "LWC_HASH_KAT_256.txt")
test_kat("test_lwc_aead_kat", "LWC_AEAD_KAT_256_128.txt")


def test(name, extra_sources=[]):
    prog = env.Program(target=name, source=[name + ".c"] + extra_sources)
    env.Command(target=name + ".txt", source=prog, action="$SOURCE ")


env_ed25519 = env.Clone()
env_ed25519.Append(CCFLAGS=["-Wno-conversion", "-fwrapv"])

test("test_x25519")
test("test_fe")
test("test_reduce", extra_sources=[env_ed25519.Object("sc_reduce.c")])
